IF
Global("bd_alt_willing_surrender","global",6)
!See("BDHepher")
THEN
RESPONSE #100
StartDialogueNoSet(Player1) 
END

IF
Global("BDEEAunplot","Global",6)
!See([ENEMY])
THEN
RESPONSE #100
MoveToObjectFollow(Player1)
END

IF
Global("BDEEAunplot","Global",7)
AreaCheck("ar4500")
See("BDAun")
Delay(12)
THEN
RESPONSE #100
MoveToObject("BDAun")
StartDialogueNoSet("BDCaelar") 
END

IF
Global("BDEEAunplot","Global",8)
Global("InParty","LOCALS",0)
InParty("BDCaelar")  
THEN
RESPONSE #100
SetGlobal("InParty","LOCALS",1)
ChangeAIScript("DPlayer2",DEFAULT)
ChangeAlignment(Myself,LAWFUL_NEUTRAL)
Wait(2)
AddJournalEntry(@14,QUEST) 
Wait(2) 
EraseJournalEntry(@12)
END

IF
Global("BDEEAunplot","Global",8)
Global("BD_ToB_CaelarD","Global",0)
!AreaCheck("ar5000")
AreaType(OUTDOOR)
CombatCounter(0)
InParty(Myself)
THEN
RESPONSE #100
SetGlobal("BD_ToB_CaelarD","Global",1)
END

IF
AreaCheck("BD5300")
Global("BD_Visited_BD5300","Global",3)
OR(3)
Range("Infopoint18",15)
Range("Infopoint19",15)
Range("Fugue_portal",30)
THEN
RESPONSE #100
SetGlobal("BD_Visited_BD5300","Global",4)
END

IF
Global("SaradushSacked","GLOBAL",1)
Global("BD_SaradushFell","Locals",0)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("BD_SaradushFell","Locals",1)
StartDialogueNoSet(Player1)
END

IF
Global("BD_Visited_BD5300","Global",6)
Global("BD_ToB_CaelarD","Global",2)
AreaType(OUTDOOR)
THEN
RESPONSE #100
SetGlobal("BD_ToB_CaelarD","Global",3)
RealSetGlobalTimer("BD_ToB_CaelarT","Locals",2100)
END

IF
OR(2)
Global("BD_ToB_CaelarD","Global",3)
Global("BD_ToB_CaelarD","Global",5)
RealGlobalTimerExpired("BD_ToB_CaelarT","Locals")
!AreaCheck("ar4500")
AreaType(DUNGEON)
THEN
RESPONSE #100
IncrementGlobal("BD_ToB_CaelarD","Global",1)
END

IF
Global("BD_Visited_BD5300","Global",3)
GlobalGT("Chapter","Global",20)
InParty(Myself)
THEN
RESPONSE #100
SetGlobal("BD_Visited_BD5300","Global",31)
END

IF
OR(5)
Global("BD_ToB_CaelarD","Global",1)
Global("BD_ToB_CaelarD","Global",4)
Global("BD_Visited_BD5300","Global",4)
Global("BD_Visited_BD5300","Global",31)
Global("BD_SaradushFell","Locals",1)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogNoSet("BDCaelar")
END

IF
GlobalGT("BD_crime","Locals",0)
InParty("Sarevok")
Global("CealarSarevok","Locals",0)
GlobalGT("BDEEAunplot","Global",7)
!AreaType(OUTDOOR)
!AreaType(DUNGEON)
Delay(24)
THEN
RESPONSE #100
SetGlobal("CealarSarevok","Locals",1)
END

IF
Global("CealarSarevok","Locals",2)
InParty("Sarevok")
GlobalGT("Chapter","Global",20)
AreaCheck("ar4500")
Delay(12)
THEN
RESPONSE #100
SetGlobal("CealarSarevok","Locals",3)
END

IF
OR(2)
Global("CealarSarevok","Locals",1)
Global("CealarSarevok","Locals",3)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See("Sarevok")
!StateCheck("Sarevok",CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogNoSet("BDCaelar")
END

IF
InParty("LRIrenic")
Global("CaeJonel","Locals",0)
!AreaCheck("ar4500")
GlobalGT("BDEEAunplot","Global",7)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("CaeJonel","Locals",1)
END

IF
InParty("LRIrenic")
Global("CaeJonel","Locals",2)
GlobalGT("Chapter","Global",20)
!AreaCheck("ar4500")
OR(3)
Dead("Sendai")
Dead("Abazigal")
Dead("Balth")
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("CaeJonel","Locals",3)
END

IF
OR(2)
Global("CaeJonel","Locals",1)
Global("CaeJonel","Locals",3)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See("LRIrenic")
!StateCheck("LRIrenic",CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogNoSet("BDCaelar")
END

IF
Global("CealarImoen","Locals",0)
InParty("Imoen2")
CombatCounter(0)
InParty(Myself)
Dead("Yaga01")
!AreaCheck("ar6203")
Delay(24)
THEN
RESPONSE #100
SetGlobal("CealarImoen","Locals",1)
END

IF
Global("CealarImoen","Locals",1)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See("Imoen2")
InParty("Imoen2")
!StateCheck("Imoen2",CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogNoSet("BDCaelar")
END

IF
Global("ViconiaImoen","Locals",0)
InParty("Viconia")
CombatCounter(0)
InParty(Myself)
OR(3)
AreaCheck("ar5200")
AreaCheck("ar6000")
AreaCheck("ar6100")
Delay(24)
THEN
RESPONSE #100
SetGlobal("ViconiaImoen","Locals",1)
END

IF
Global("ViconiaImoen","Locals",1)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See("Viconia")
InParty("Viconia")
!StateCheck("Viconia",CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogNoSet("BDCaelar")
END